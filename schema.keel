model Itinerary {
    fields {
        // Choose your location, number of days for the trip, what activities you want to plan - 
        numberOfDays Number
        location Text
        activityTypes ActivityType[]
        notes Text?
    }

    actions {
        create createItinerary() with (numberOfDays, location, activityTypes)
        update updateItineraryNotes(id) with (notes)
    }

    @permission(
        actions: [get, list, update, delete, create],
        expression: true 
    )
}

enum ActivityType {
    See
    Do
    Eat
    Stay
}

job UpdateCheapestFlightsAnywhere {
    @schedule("0 8 * * *")
    @permission(expression: true)
}

model Destination {
    fields {
        entityId Text
        name Text
    }

    actions {
        read autoSuggestDestinations(DestinationQuery) returns (AutoSuggestResponse) {
            @permission(expression: true)
            @function
        }
    }
}

message DestinationQuery {
   destination Text
}

message AutoSuggestResponse {
  places Places[]
}

message Places {
    entityId Text
    iataCode Text
    name Text
    countryId Text
    countryName Text
    cityName Text
    location Text
    hierarchy Text
    type Text
}

model CheapestFlightsAnywhere {
    fields {
        origin Text
        destination Text
        cost Number
    }

    actions {
        list allFlights()
    }

    @permission(
        actions: [get, list, update, delete],
        expression: true 
    )
}



model Activity {
    fields {
        title Text
        type ActivityType
        details Text?
        placeId Text? 
    }
}

api Api {
    models {
        CheapestFlightsAnywhere
        Itinerary
        Activity
        Destination
    }
}
